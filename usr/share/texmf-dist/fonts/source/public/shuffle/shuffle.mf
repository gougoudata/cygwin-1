% This is shuffle.mf for creating a shuffle product symbol in the
% same format as the CM symbols.  It is based on the cmsy font.
% Written by Julian Gilbey <J.D.Gilbey@qmul.ac.uk>
% This file is in the public domain

font_coding_scheme:="TeX math symbols";
mode_setup; font_setup;

font_slant slant; font_x_height x_height#;
font_quad 18u# if not monospace:+4letter_fit# fi;
% (The spacing is zero so that \TeX\ will always add the italic correction.)
% (The calling file should give the other math symbol font parameters.)

slant:=mono_charic#:=0;  % the remaining characters will not be slanted
currenttransform:=identity yscaled aspect_ratio scaled granularity;

cmchar "Shuffle product sign";
beginchar(oct"001",16u#,.7asc_height#,0);
adjust_fit(0,0); pickup rule.nib;
lft x1=hround 1.1u; x2=x1; x4=x5=w-x1;
x3=good.x 0.5[x2,x4]; x6=x3;
y1=good.y h+o; bot y2=0; y4=y2; y5=y1;
y3=y2; y6=y1;
draw z1---z2---z4---z5;  % stems and bar
draw z3--z6; % middle bar
labels(1,2,3,4,5,6); endchar;

cmchar "Complete shuffle product sign";
beginchar(oct"002",16u#,asc_height#,0);
adjust_fit(0,0); pickup rule.nib;
lft x1=hround 1.1u; x2=x1; x4=x5=w-x1;
x3=good.x 0.5[x2,x4]; x6=x3;
y1=good.y 7/10h+o; bot y2=0; y4=y2; y5=y1;
y3=y2; y6=y1;
x7=good.x 0.05[x2,x4]; x8=w-x7;
y7=good.y h; y8=y7;
draw z1---z2---z4---z5;  % stems and bar
draw z3--z6; % middle bar
draw z7--z8; % overline
labels(1,2,3,4,5,6,7,8); endchar;

bye.
